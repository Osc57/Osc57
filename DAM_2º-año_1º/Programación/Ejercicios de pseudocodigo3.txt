Ej1
Proceso Factorial
	Definir num,contador Como Entero;
	Definir resultado Como Real;
	resultado <- 1;
	Escribir Sin Saltar "Dime un número:";
	Leer num;
	contador <- 2;
	Mientras contador<=num Hacer
		resultado <- resultado * contador;
		contador <- contador + 1;
	FinMientras
	Escribir "El resultado es ", resultado;
FinProceso

Ej2
Proceso AdivinarNumero
	Definir num,rNum,cont Entero;
	Escribir "Adivina el número (1-100)";
	Leer num;
	rNum <- Aleatorio(1-100);

	Mientras rNum <> num Hacer
		Si num > rNum Entonces
			Escribir "El número es menor"
		SiNo
			Escribir "El número es mayor"
		FinSi
		cont <- cont-1;
		 Escribir "Le quedan ",intentos," intentos:";
        	Leer num;
    	FinMientras
    
    	Si num_secreto=num_ingresado Entonces
        	Escribir "Exacto! Usted adivino en ",11-intentos," intentos.";
    	Sino
        	Escribir "El numero era: ",rNum;
    	FinSi
FinProceso

Ej3
Proceso PedirNúmeros
	Definir num,suma,cont,media Entero;
	Escribir "Dime un número";
	Leer numero;
	cont<-0;
	suma<-0;
	Mientras num<>0 Hacer
		suma <- suma + num;
		cont <- cont + 1;
		Leer num;
	FinMientras
	Si cont>0 Entonces
		media <- suma/cont;
	SiNo
		media <-0;
	FinSi
	Escribir "La suma es: " suma;
	Escribir "La media es: " media;
FinProceso

Ej4
Proceso AlgoritmoDeNumeros
	Definir num Como Entero;
	Definir cantidad_num,i Como Entero;
	Definir cont_negativos,cont_positivos,cont_ceros Como Entero;
	cont_negativos <- 0;
	cont_positivos <- 0;
	cont_ceros <- 0;
	Escribir "¿Cuántos números vas a introducir?:";
	Leer cantidad_num;
	Para i<-1 Hasta cantidad_num Hacer
		Escribir "Número ",i,":" ;
		Leer num;
		Si num>0 Entonces
			cont_positivos <- cont_positivos + 1;
		Sino 
			Si num<0 Entonces
				cont_negativos <- cont_negativos + 1;
			SiNo
				cont_ceros <- cont_ceros + 1;
			FinSi
		FinSi
	FinPara
	Escribir "Números positivos:",cont_positivos;
	Escribir "Números negativos:",cont_negativos;
	Escribir "Números igual a 0:",cont_ceros;
FinProceso

Ej5
Proceso Caracteres
	Definir car Como Caracter;
	Repetir
		Repetir
			Escribir "Introduce un carácter:";
			Leer car;
		Hasta que Longitud(car)=1;
		Si car<>" " Entonces
			Si Mayusculas(car)="A" o Mayusculas(car)="E" o Mayusculas(car)="I" o Mayusculas(car)="O" o Mayusculas(car)="U" Entonces
				Escribir "VOCAL";
			SiNO
				Escribir "NO VOCAL";
			FinSi
		FinSi
	Hasta Que car=" ";
FinProceso

Ej6
Proceso NúmerosPares
	Definir num,num1,num2 Como Entero;
	Escribir "Introduce el número 1:";
	Leer num1;
	Escribir "Introduce el número 2:";
	Leer num2;
	Si num1 % 2 = 1 Entonces
		num1<-num1+1;
	FinSi
	Para num<-num1 Hasta num2 Con Paso 2 Hacer
			Escribir num," ";
	FinPara 
FinProceso

Ej7
Proceso TablaMultiplicar
	Definir tabla,i Como Entero;
	Escribir "¿De qué número quieres mostrar la tabla de multiplicar?:";
	Leer tabla;
	Para num<-1 hasta 10 Hacer
		Escribir num," * ",tabla," = ",num*tabla;
	FinPara
FinProceso

Ej8
Proceso Limite
	Definir lim_inf, lim_sup como Entero;
	Definir num como Entero;
	Definir suma_dentro_intervalo Como Entero;
	Definir cont_fuera_intervalo Como Entero;
	Definir igual_limites Como Logico;
	suma_dentro_intervalo <- 0;
	cont_fuera_intervalo <- 0;
	igual_limites <- Falso;
	Repetir
		Escribir "Introduce el límite inferior del intervalo:";
		Leer lim_inf;
		Escribir "Introduce el límite superior del intervalo:";
		Leer lim_sup;
		Si lim_inf>lim_sup Entonces
			Escribir "ERROR: El límite inferior debe ser menor que el superior.";
		FinSi
	Hasta Que lim_inf<=lim_sup;
	
	Escribir Sin Saltar "Introduce un número (0, para salir):";
	Leer num;
	Mientras num<>0 Hacer
		//Pertenece al intervalo
		Si num>lim_inf Y num<lim_sup Entonces
			suma_dentro_intervalo <- suma_dentro_intervalo + num;
		SiNo
			//No pertenece al intervalo
			cont_fuera_intervalo <- cont_fuera_intervalo + 1;
		FinSi
		//Número igual a alguno de los límites
		Si num=lim_inf O num=lim_sup Entonces
			igual_limites <- Verdadero;
		FinSi
		Escribir Sin Saltar "Introduce un número (0, para salir):";
		Leer num;
	FinMientras
	Escribir "La suma de los número dentro del intervalo es ",suma_dentro_intervalo;
	Escribir "La cantidad de números fuera del intervalo es ",cont_fuera_intervalo;
	Si igual_limites Entonces
		Escribir "Se ha introducido algún número igual a los límites del intervalo.";
	SiNo
		Escribir "No se ha introducido ningún número igual a los límites del intervalo.";
	FinSi
FinProceso

Ej9
Proceso Potencia
	Definir base, potencia Como Real;
	Definir exponente Como Entero;
	Definir i Como Entero;
	Escribir Sin Saltar "Dame la base de la potencia:";
	Leer base;
	Repetir
		Escribir Sin Saltar "Dame el exponente de la potencia:";
		Leer exponente;
		Si exponente<0 Entonces
			Escribir "ERROR: El exponente debe ser positivo";
		FinSi
	Hasta Que exponente >= 0;
	potencia <- 1;
	Para i<-1 hasta exponente Hacer
		potencia <- potencia * base;
	FinPara
	Escribir "Potencia:",potencia;
FinProceso

Ej10
Proceso AlgoritmoMultiplicar
	Definir tabla,num Como Entero;
	Para tabla<-1 hasta 5 Hacer
		Para num<-1 hasta 10 Hacer
			Escribir tabla," * ",num," = ", tabla*num;
		FinPara
		Esperar Tecla;
	FinPara
FinProceso

Ej11
Proceso PrimoONo
	Definir numero_es_primo,num Como Entero;
	Definir es_primo Como Logico;
	es_primo<-Verdadero;
	Escribir "Introduce un número para comprobar si es primo:";
	Leer numero_es_primo;
	Para num<-2 hasta rc(numero_es_primo) Hacer
		Si numero_es_primo % num = 0 Entonces
			es_primo <- Falso;
		FinSi
	FinPara
	Si es_primo Entonces
		Escribir "Es Primo";
	SiNo
		Escribir "No es Primo";
	FinSi
FinProceso

Ej12
Proceso Ahorra
	Definir cant_mensual Como Real;
	Definir ahorro_acum como Real;
	Definir mes Como Entero;
	ahorro_acum<-0;
	Para mes<-1 hasta 12 Hacer
		Escribir "¿Cuánto has ahorrado en el mes ",mes,"?:";
		Leer cant_mensual;
		ahorro_acum <- ahorro_acum + cant_mensual;
		Escribir "En el mes ",mes," llevas ahorrado ",ahorro_acum;
	FinPara 
FinProceso

Ej13
Proceso HorasDeTrabajo
	Definir sueldo_por_hora Como Real;
	Definir horas,horas_acum como Entero;
	Definir dia Como Entero;
	horas_acum<-0;
	Escribir "Introduce el sueldo por hora:";
	Leer sueldo_por_hora;
	Para dia<-1 hasta 6 Hacer
		Escribir "¿Cuántas horas has trabajado el día ",dia,"?:";
		Leer horas;
		horas_acum <- horas_acum + horas;
	FinPara
	Escribir "Horas acumuladas en la semana:",horas_acum;
	Escribir "Sueldo:",sueldo_por_hora*horas_acum;
FinProceso

Ej14
Proceso Kilometros
	Definir km1,km2 Como Entero;
	km1<-70;
	km2<-150;
	Mientras km1<>km2 Hacer
		km1<-km1+1;
		km2<-km2-1;
	FinMientras
	
	Escribir "Se encuentran en el km:",km1;
FinProceso

Ej15
Proceso PagarMensualmente
	Definir pago, pago_acum Como Real;
	Definir mes Como Entero;
	pago_acum<-0;
	pago<-10;
	Para mes <- 1 Hasta 20 Hacer
		pago_acum<-pago_acum+pago;
		pago<-pago*2;
	FinPara
	Escribir "Al final de los 20 meses tuvo que pagar: ",pago_acum;
FinProceso

Ej16
Proceso SueldoSemanal
	Definir horas_por_semana, horas_acum Como Entero;
	Definir sueldo_por_hora Como Real;
	Definir trabajadores,trabajador como Entero;
	horas_acum<-0;
	Escribir "¿Cuántos trabajadores tiene la empresa?:";
	Leer trabajadores;
	Escribir "Sueldo por hora:";
	Leer sueldo_por_hora;
	Para trabajador<-1 hasta trabajadores Hacer
		Escribir "¿Cuántas horas ha trabajado el trabajador ",trabajador," ?";
		Leer horas_por_semana;
		horas_acum<-horas_acum+horas_por_semana;
		Escribir "El trabajador ",trabajador," tiene de sueldo ",horas_por_semana*sueldo_por_hora; 
	FinPara
	Escribir "El pago a los ",trabajadores," trabajadores es: ",horas_acum*sueldo_por_hora;
FinProceso

Ej17
Proceso PagoEmpresa
	Definir dias,horas Como Entero;
	Definir horas_por_trabajador,horas_acum Como Entero;
	Definir dia, trabajador Como Entero;
	Definir sueldo_por_hora Como Real;
	Definir trabajadores como Entero;
	
	Escribir "¿Cuántos trabajadores tiene la empresa?:";
	Leer trabajadores;
	Escribir "Sueldo por hora:";
	Leer sueldo_por_hora;
	horas_acum<-0;
	Para trabajador<-1 hasta trabajadores Hacer
		horas_por_trabajador<-0;
		Escribir "¿Cuántos dí­as ha trabajado el trabajador ",trabajador," ?";
		Leer dias;
		Para dia<-1 hasta dias Hacer
			Escribir "¿Cuántas horas ha trabajado el trabajador ",trabajador," el día ",dia,"?:";
			Leer horas;
			horas_por_trabajador<-horas_por_trabajador+horas;
		FinPara
		Escribir "El trabajador ",trabajador," tiene de sueldo ",horas_por_trabajador*sueldo_por_hora; 
		horas_acum<-horas_acum+horas_por_trabajador;
	FinPara
	Escribir "El pago a los ",trabajadores," trabajadores es :",horas_acum*sueldo_por_hora;
FinProceso

Ej18
Proceso Cronometro
	Definir hora,minuto,segundo como Entero;
	Para hora <- 0 hasta 23 con paso 1 hacer 
		Para minuto <- 0 hasta 59 con paso 1 hacer 
			Para segundo <- 0 hasta 59 con paso 1 hacer 
				Limpiar Pantalla;
				Escribir hora,":",minuto,":",segundo;
				Esperar 1 Segundo;
			finpara 
		finpara
	finpara
FinProceso

Ej19
Proceso Menu
	Definir opcion Como Entero;
    Repetir
        Limpiar Pantalla;
        Escribir "Menú de recomendaciones";
        Escribir "   1. Literatura";
        Escribir "   2. Cine";
        Escribir "   3. Música";
        Escribir "   4. Videojuegos";
        Escribir "   5. Salir";
        Escribir "Elija una opción (1-5): ";
        Leer opcion;
        Segun opcion Hacer
            1:
                Escribir "Lecturas recomendables:";
                Escribir " + Esperándolo a Tito y otros cuentos de fúbol (Eduardo Sacheri)";
                Escribir " + El juego de Ender (Orson Scott Card)";
                Escribir " + El sueño de los héroes (Adolfo Bioy Casares)";
            2:
                Escribir "Películas recomendables:";
                Escribir " + Matrix (1999)";
                Escribir " + El último samuray (2003)";
                Escribir " + Cars (2006)";
            3:
                Escribir "Discos recomendables:";
                Escribir " + Despedazado por mil partes (La Renga, 1996)";
                Escribir " + Búfalo (La Mississippi, 2008)";
                Escribir " + Gaia (Mägo de Oz, 2003)";
            4:
                Escribir "Videojuegos clásicos recomendables";
                Escribir " + Día del tentáculo (LucasArts, 1993)";
                Escribir " + Terminal Velocity (Terminal Reality/3D Realms, 1995)";
                Escribir " + Death Rally (Remedy/Apogee, 1996)";
            5:
                Escribir "Gracias, vuelva prontos";
            De otro modo:
                Escribir "Opción no válida";
        FinSegun
        Escribir "Presione enter para continuar";
        Esperar Tecla;
    Hasta Que opcion=5
FinProceso

Ej20
Fin PrimerosNumerosPrimosHastaN
	Definir cant_a_mostrar,num,cant_mostrados,divisor Como Entero;
    Definir es_primo Como Logico;
    Repetir
		Escribir "Ingrese la cantidad de números primos a mostrar:";
		Leer cant_a_mostrar;
		Hasta que cant_a_mostrar>0;
    
    Escribir "1: 2"; 
    cant_mostrados <- 1;
    num<-3;                
    Mientras cant_mostrados<cant_a_mostrar Hacer
        es_primo <- Verdadero; 
        Para divisor<-3 hasta rc(num) con paso 2 Hacer 
            Si num % divisor = 0 entonces // si la división da exacta...
                es_primo <- Falso;  
            FinSi
        FinPara
		
        Si es_primo Entonces
            cant_mostrados <- cant_mostrados + 1;
            Escribir cant_mostrados, ": ",num;
        FinSi
		num <- num + 2 ;
    FinMientras
FinProceso









































			
			